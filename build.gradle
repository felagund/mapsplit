

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.com.github.breadmoirai:github-release:2.2.11"
    }
}

plugins {
    id "org.sonarqube" version "4.0.0.2929"
}

apply plugin: "com.github.breadmoirai.github-release"
apply plugin: 'java'
apply plugin: "jacoco"
apply plugin: 'eclipse'

version = '0.4.0'

repositories {
    mavenCentral()
    maven { url 'https://nexus.onebusaway.org/nexus/content/repositories/public/' }
}

ext {
    github_token = System.getenv('GITHUB_TOKEN')
}

//create a single Jar with all dependencies
task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'mapsplit fat jar',
        'Implementation-Version': "${project.version}",
        'Main-Class': 'dev.osm.mapsplit.MapSplit'
    }
    archiveBaseName = project.name + '-all'
    from { configurations.implementation.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
fatJar.group = 'build'

jacocoTestReport {
    reports {
        xml.required=true
    }
}

sonarqube {
    properties {
        property "sonar.java.source","1.8"
        property "sonar.sourceEncoding","UTF-8"
        property "sonar.java.binaries", "build/classes/java/main/"
        property "sonar.coverage.jacoco.xmlReportPaths","build/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.qualitygate.wait", "true"
    }
}

test{
    maxHeapSize = "6G"
}

dependencies {
    implementation 'org.openstreetmap.osmosis:osmosis-core:0.47'
    implementation 'org.openstreetmap.osmosis:osmosis-pbf:0.47'
    implementation 'com.google.protobuf:protobuf-java:3.7.1'
    implementation 'commons-cli:commons-cli:1.4'
    implementation 'ch.poole.geo:mbtiles4j:1.2.0'
    implementation 'org.jetbrains:annotations:15.0'
    implementation 'com.zaxxer:SparseBitSet:1.1'
    testImplementation 'junit:junit:4.12'
}

githubRelease {
    token "${github_token}"
    owner "simonpoole"
    tagName "${version}"
    releaseName "${version}"
    body ""
    FilenameFilter filter = { dir, filename -> filename.contains(version) }
    releaseAssets = new File(buildDir.getPath() + '/libs').listFiles filter
    overwrite true
}
